buildscript {
    repositories {
        mavenCentral()
        maven {
            url("https://plugins.gradle.org/m2/")
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.4.0.RELEASE")
        classpath("gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:1.4.17")
    }
}

allprojects {

    repositories {
        mavenCentral()
    }

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'eclipse'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    group = 'org.hmhb'
    version = '1.0-SNAPSHOT'

}

/* This plugin adds git info into the /info actuator. */
apply plugin: "com.gorylenko.gradle-git-properties"

apply plugin: 'spring-boot'

springBoot {
    /* This adds the build info into the /info actuator. */
    buildInfo()
}

jar {
    baseName = 'hmhb-server'

    manifest {
        attributes(
                "Implementation-Title": baseName,
                "Implementation-Version": version
        )
    }
}

dependencies {

    compile project(':client')

    /* brings in what spring-boot needs for a webapp (we'll be running tomcat) */
    compile("org.springframework.boot:spring-boot-starter-web")

    /* brings in the spring-boot "actuators" -- production ready servlets offering metrics, etc. */
    compile("org.springframework.boot:spring-boot-starter-actuator")

    /* allows us to have the /docs that explains spring-boots' actuators */
    compile("org.springframework.boot:spring-boot-actuator-docs")

    /* brings in most of what's needed for JPA in spring-boot */
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.postgresql:postgresql:9.4-1201-jdbc4")

    /* needed for hibernate-validator, which depends on javax.el-api */
    compile("org.glassfish:javax.el")

    /* slf4j logging */
    compile("org.slf4j:slf4j-api:1.7.21")

    /* for the simple reflection based equals/hashCode/toString methods */
    compile("org.apache.commons:commons-lang3:3.4")

    /* adds metrics and spring interceptor so we can use @Timed */
    compile("io.dropwizard.metrics:metrics-core:3.1.2")
    compile("com.ryantenney.metrics:metrics-spring:3.1.3")

    /* adds in google's findbugs jar so @Nonnull and @Nullable can be used */
    compile("com.google.code.findbugs:jsr305:3.0.1")

    testCompile("junit:junit:4.12")
    testCompile("org.hamcrest:hamcrest-core:1.3")
    testCompile("org.mockito:mockito-all:1.10.19")
    testCompile("org.springframework.boot:spring-boot-starter-test")

}
